# Maintainer: Doug Newgard <scimmia at archlinux dot org>
# Contributor: Jiachen Yang <farseerfc@gmail.com>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Ferik <djferik at gmail dot com>

pkgname=masterpdfeditor
pkgver=5.4.38
pkgrel=1
pkgdesc='A complete solution for creation and editing PDF files'
url='https://code-industry.net/free-pdf-editor/'
arch=('x86_64')
license=('custom')
makedepends=('patchelf')
source_x86_64=("https://code-industry.net/public/master-pdf-editor-${pkgver}-qt5-all.amd64.tar.gz" "masterpdfeditor.sh.patch")
sha1sums_x86_64=('bff9d66b531e60603a9221cf6bd2595c6bf602d6'
                 'b82145268374e0db5eb7c6673e03971de8289bec')
prepare() {
    cd "$srcdir/master-pdf-editor-${pkgver%%.*}"
    patch -p1 -i "$srcdir/masterpdfeditor.sh.patch"
}

package() {
  depends=('gcc-libs' 'glibc' 'libgl' 'nspr' 'nss' 'qt5-base' 'qt5-svg' 'sane')

  install -d "$pkgdir"{/opt/,/usr/bin/}
  cp -a --no-preserve=ownership master-pdf-editor-${pkgver%%.*} "$pkgdir/opt/"

  cd "$pkgdir/opt/master-pdf-editor-${pkgver%%.*}"
  ln -sr masterpdfeditor${pkgver%%.*}.sh "$pkgdir/usr/bin/masterpdfeditor${pkgver%%.*}"
  install -Dm644 masterpdfeditor${pkgver%%.*}.desktop -t "$pkgdir/usr/share/applications/"
  install -Dm644 license.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  patchelf --remove-rpath masterpdfeditor${pkgver%%.*}
}
