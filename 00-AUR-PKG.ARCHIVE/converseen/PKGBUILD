# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.9.6.2
pkgrel=5
pkgdesc="Batch image converter and resizer"
arch=('x86_64')
url="http://converseen.fasterland.net/"
license=('GPL3')
depends=('ghostscript' 'libmagick' 'libraw' 'librsvg' 'libwebp' 'libwmf' 'openexr' 'qt5-base')
makedepends=('cmake' 'imagemagick' 'qt5-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('f67d858519f26167bafd50f4c31181801784df279d893a6e1dfc31cd45d21187')

build() {
  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
